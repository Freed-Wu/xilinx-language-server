""
" @section Introduction, intro
" @library
" <doc/@plugin(name).txt> is generated by <https://github.com/google/vimdoc>.
" See <README.md> for more information about installation and screenshots.

""
" Refer $VIMRUNTIME/autoload/dist/script.vim in vim
" or $VIMRUNTIME/scripts.vim in neovim. When a file's shebang is like
" "xsct" or "vivado -mode batch -source", set its filetype to |xsct| or |xdc|.
function! xilinx#shebang() abort
  let s:line1 = getline(1)

  if s:line1 =~# '^#!'
    if s:line1 =~# '^#!\s*\S*\<env\s'
      let s:line1 = substitute(s:line1, '\S\+=\S\+', '', 'g')
      let s:line1 = substitute(s:line1, '\(-[iS]\|--ignore-environment\|--split-string\)', '', '')
      let s:line1 = substitute(s:line1, '\<env\s\+', '', '')
    endif
    if s:line1 =~# '^#!\s*\a:[/\\]'
      let s:name = substitute(s:line1, '^#!.*[/\\]\(\i\+\).*', '\1', '')
    elseif s:line1 =~# '^#!.*\<env\>'
      let s:name = substitute(s:line1, '^#!.*\<env\>\s\+\(\i\+\).*', '\1', '')
    elseif s:line1 =~# '^#!\s*[^/\\ ]*\>\([^/\\]\|$\)'
      let s:name = substitute(s:line1, '^#!\s*\([^/\\ ]*\>\).*', '\1', '')
    else
      let s:name = substitute(s:line1, '^#!\s*\S*[/\\]\(\i\+\).*', '\1', '')
    endif
    if s:name =~# '^vivado'
      set filetype=xdc
    elseif s:name =~# '^xsct'
      set filetype=xsct
    endif
  endif
endfunction
